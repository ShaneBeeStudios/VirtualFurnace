plugins {
    id("java")
    id("com.gradleup.shadow") version "9.2.0"
    id("maven-publish")
}

// VirtualFurnace version
version = "1.1.2"
group = "com.github.ShaneBeeStudios"
val apiVersion = "1.13"

java.sourceCompatibility = JavaVersion.VERSION_21

repositories {
    mavenCentral()
    mavenLocal()

    // Paper
    maven("https://repo.papermc.io/repository/maven-public/")
}
dependencies {
    // Paper
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")

    // bStats
    implementation("org.bstats:bstats-bukkit:3.1.0")
}

tasks {
    javadoc {
        title = "VirtualFurnace API " + project.version
        options.encoding = Charsets.UTF_8.name()
        exclude("com/shanebeestudios/vf/**.java", "com/shanebeestudios/vf/command", "com/shanebeestudios/vf/debug")
        (options as StandardJavadocDocletOptions).links(
            "https://javadoc.io/doc/org.jetbrains/annotations/latest/",
            "https://jd.papermc.io/paper/1.21.11/",
            "https://jd.advntr.dev/api/4.25.0/",
        )
    }
    processResources {
        expand("version" to version, "apiversion" to apiVersion)
    }
    compileJava {
        options.release = 21
        options.compilerArgs.add("-Xlint:unchecked")
        options.compilerArgs.add("-Xlint:deprecation")
    }
    shadowJar {
        archiveFileName = project.name + "-" + project.version + ".jar"
        relocate("org.bstats", "com.shanebeestudios.vf.metrics")
        exclude("META-INF/**", "LICENSE")
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        // Create a publication named 'shadow' (or any name you prefer)
        create<MavenPublication>("shadow") {
            // Use the component generated by the Shadow plugin
            from(components["java"])
        }
    }
    // Define the repository where you want to publish the JAR
    repositories {
        // To publish to your local Maven repository (~/.m2)
        mavenLocal()
    }
}
